apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group POM_ARTIFACT_GROUP
version KROID_VERSION

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode KROID_VERSION_CODE
        versionName KROID_VERSION
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        kroid(MavenPublication) {
            groupId POM_ARTIFACT_GROUP
            artifactId project.name
            version KROID_VERSION

            artifact sourcesJar
            artifact bundleRelease

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'kroid'
                    description 'Kotlin DSL for Android'
                    url PROJECT_URL

                    scm {
                        url SCM_URL
                        connection CONNECTION_URL
                        developerConnection DEVELOPER_CONNECTION_URL
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                            comments 'A business-friendly OSS license'
                        }
                    }
                    developers {
                        developer {
                            id 'mo3in'
                            name 'Mo3in'
                            email 'mo3in.hente@gmail.com'
                            site 'https://mo3in.me'
                        }
                    }
                }

                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.api.dependencies.each { Dependency dep ->
                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    println dep.version + ", " + dep.group
                    if (dep.version == 'unspecified' || dep.group == 'unspecified') {
                        dependencyNode.appendNode('version', KROID_VERSION)
                        dependencyNode.appendNode('groupId', POM_ARTIFACT_GROUP)
                    } else {
                        dependencyNode.appendNode('version', dep.version)
                        dependencyNode.appendNode('groupId', dep.group)
                    }
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('scope', "compile")
                }

                configurations.implementation.dependencies.each { Dependency dep ->
                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', "runtime")
                }
            }
        }
    }

    repositories {
        maven { url "${rootProject.buildDir}/maven" }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: ""
    key = System.getenv('BINTRAY_API_KEY') ?: ""

    publications = ['kroid']

    dryRun = BINTRAY_DRY_RUN

    pkg {
        userOrg = BINTRAY_USER_NAME
        repo = BINTRAY_REPO_NAME
        name = BINTRAY_PACKAGE_NAME
        licenses = ['MIT']
        vcsUrl = SCM_URL

        version {
            name = KROID_VERSION
            released = new Date()
        }
    }
}